#  1.123 2.1-16
# Выполнил: Маковеев Никита Владимирович.
# Дата выполнения: 03.05.2025.
# Начало: 23:30.
# Конец: 00:11.
# Атака с использованием общего модуля.
######################################################################################
with(NumberTheory):
with(ArrayTools):
# К информационному обмену подключается злоумышленник(Ева),
# У абонентов сети одинаковый модуль N. m - текст, который хочет взломать Ева.
# Пусть Еве удалось вынудить Алису и Боба зашифровать один и тот же текст.
# (a, b) = 1, a - Открытый ключ Алисы, b - открытый ключ Боба
#---------------------------------------------------------
# Подготовим алгоритм RSA.
P := nextprime(rand(10^30 .. 10^50)());
Q := nextprime(rand(10^30 .. 10^50)());
N := P*Q; phB := (P-1)*(Q-1);
m := rand(1 .. N)();
b := rand(2 .. phB)();
while igcd(b, phB) <> 1 do
b := rand(2 .. phB)();
end do;
#  b, N - открытый ключ Боба.
#---------------------------------------------------------
# Вычислим ключи Алисы.
phA := (P-1)*(Q-1);
a := rand(2 .. phA)();
while igcd(a, phA) <> 1 or igcd(a, b) <> 1 do
a := rand(2 .. phA)();
end do;
# a, N - открытый ключ Алисы.
#---------------------------------------------------------
# Ева получает зашифрованные сообщения
c1 := Power(m, a) mod N;
c2 := Power(m, b) mod N;
#---------------------------------------------------------
# Получаем закрытые ключи с помощью Алгоритма Евклида.
igcdex(a,b, 'x', 'y');
#---------------------------------------------------------
# Теперь зная x и y, можем найти m
m1 := c1&^x * c2&^y mod N;
m-m1;
